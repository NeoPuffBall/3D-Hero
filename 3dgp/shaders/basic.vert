// VERTEX SHADER

#version 330
#define MAX_BONES 100

uniform mat4 matrixProjection;
uniform mat4 matrixView;
uniform mat4 matrixModelView;
uniform mat4 bones[MAX_BONES];

// Materials
uniform vec3 materialAmbient;
uniform vec3 materialDiffuse;
uniform vec3 materialSpecular;
uniform float shininess;

in vec3 aVertex;
in vec3 aNormal;
in vec2 aTexCoord;
in ivec4 aBoneId;
in vec4 aBoneWeight;

out vec4 color;
out vec2 texCoord0;

out vec4 position;
out vec3 normal;

uniform float fogDensity;

out float fogFactor;

struct AMBIENT
{
	vec3 color;
};
uniform AMBIENT lightAmbient;

struct DIRECTIONAL
{
vec3 direction;
vec3 diffuse;
};
uniform DIRECTIONAL lightDir;

vec4 AmbientLight(AMBIENT light)
{
	return vec4(materialAmbient * light.color, 1);
}

vec4 DirectionalLight(DIRECTIONAL light)
{
// Calculate Directional Light
vec4 color = vec4(0, 0, 0, 0);
vec3 L = normalize(mat3(matrixView) * light.direction);
float NdotL = dot(normal, L);
color += vec4(materialDiffuse * light.diffuse, 1) * max(NdotL, 0);
return color;
}

void main(void) 
{
    mat4 matrixBone;
    if (aBoneWeight[0] == 0.0)
        matrixBone = mat4(1);
    else
        matrixBone = (bones[aBoneId[0]] * aBoneWeight[0] +
        bones[aBoneId[1]] * aBoneWeight[1] +
        bones[aBoneId[2]] * aBoneWeight[2] +
        bones[aBoneId[3]] * aBoneWeight[3]);

    // calculate position
    position = matrixModelView * matrixBone * vec4(aVertex, 1.0);
    gl_Position = matrixProjection * position;

    // calculate normal
    normal = normalize(mat3(matrixModelView) * mat3(matrixBone) * aNormal);

    // calculate light
    color = vec4(0, 0, 0, 1);
    color += AmbientLight(lightAmbient);
    color += DirectionalLight(lightDir);

    fogFactor = exp2(-fogDensity * length(position));

    texCoord0 = aTexCoord;
}
